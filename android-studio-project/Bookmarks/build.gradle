apply from: '../constants.gradle'
apply plugin: 'com.android.application'

android {
  compileSdkVersion project.ext.compileSdkVersion
  buildToolsVersion project.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility project.ext.javaVersion
    targetCompatibility project.ext.javaVersion
  }

  defaultConfig {
    minSdkVersion project.ext.minSdkVersion
    targetSdkVersion project.ext.targetSdkVersion

    applicationId "com.github.warren_bank.bookmarks"
    versionName project.ext.releaseVersion
    versionCode project.ext.releaseVersionCode

    archivesBaseName += "-$versionName"
  }

  buildTypes {
    release {
      shrinkResources true
      minifyEnabled   true
      proguardFiles = [
        "proguard-rules.txt",
        getDefaultProguardFile('proguard-android.txt')
      ]
    }
    debug {
      debuggable  true
      jniDebuggable true
    }
  }

  flavorDimensions "languageResources", "runtimePermissions"

  productFlavors {
    withAllLanguageTranslations {
      dimension "languageResources"
    }
    english {
      dimension "languageResources"
    }
    noRuntimePermissions {
      dimension "runtimePermissions"
      buildConfigField "boolean", "ALLOW_RUNTIME_PERMISSIONS_USER",   "false"
      buildConfigField "boolean", "ALLOW_RUNTIME_PERMISSIONS_SYSTEM", "false"
    }
    withUserRuntimePermissions {
      dimension "runtimePermissions"
      buildConfigField "boolean", "ALLOW_RUNTIME_PERMISSIONS_USER",   "true"
      buildConfigField "boolean", "ALLOW_RUNTIME_PERMISSIONS_SYSTEM", "false"
    }
    withSystemRuntimePermissions {
      dimension "runtimePermissions"
      buildConfigField "boolean", "ALLOW_RUNTIME_PERMISSIONS_USER",   "true"
      buildConfigField "boolean", "ALLOW_RUNTIME_PERMISSIONS_SYSTEM", "true"
    }
  }

  lintOptions {
    disable 'MissingTranslation'
    abortOnError true
  }
}
